package BarrackObjects

import BarrackConstants
import ChannelAbilityPreset
import UpgradeObjEditing
import Ids

@compiletime function generate()
	let ids = [BARRACK_ID_LEVEL_1,BARRACK_ID_LEVEL_2,BARRACK_ID_LEVEL_3,BARRACK_ID_LEVEL_4]
	let slotIDs = [BARRACK_SLOT_1_ID,BARRACK_SLOT_2_ID,BARRACK_SLOT_3_ID,BARRACK_SLOT_4_ID]
	
	// Worker Hall
	for i = 0 to 3
		var abilities = BARRACK_ABILITIES
		for ii = 0 to i
			if abilities.length() > 0
				abilities += ","
			abilities += slotIDs[ii].toRawCode()
		let b = new BuildingDefinition(ids[i], UnitIds.humanbarracks)
		..setNameEditorSuffix("(Barrack)")
		..setName(BARRACK_NAME+" - Level "+(i+1).toString())
		..setTooltipBasic((i==0?"|cffFFCC00Build|r":"|cffFFCC00Upgrade|r")+" "+BARRACK_TOOL_TIP_BASIC)
		..setTooltipExtended(BARRACK_TOOL_TIP_EXTENDED)
		// Icon / Model
		..setIconGameInterface(BARRACK_ICON)
		..setModelFile(BARRACK_MODEL)
		// Button Placement
		..setButtonPositionX(i != 0 ? 3 : BARRACK_BUTTON_X)
		..setButtonPositionY(i != 0 ? 2 : BARRACK_BUTTON_Y)
		// Abilities / Upgrades / Building / Training / Selling
		..setNormalAbilities(abilities)
		..setResearchesAvailable(commaList(
			BARRACK_FOOTMAN_ARMOR_ID, 
			BARRACK_FOOTMAN_DAMAGE_ID, 
			BARRACK_ARCHER_DAMAGE_ID
		))
		..setUnitsTrained(commaList(BARRACK_UNITS_TRAINED))
		..setUnitsSold(BARRACK_UNITS_SOLD)
		// Build / Cost
		..setBuildTime(1)
		..setGoldCost(BARRACK_COST_GOLD+i*BARRACK_COST_GOLD_INCREMENT)
		..setLumberCost(i==0 ? BARRACK_COST_LUMBER : 0)
		// Health
		..setHitPointsMaximumBase(1000)
		// Other
		..setIsaBuilding(true)
		..setHideMinimapDisplay(true)
		// Upgrades To
		if i < 3
			b..setUpgradesTo(commaList(ids[i+1]))
			
		if i == 0
			// Hotkey
			b..setHotkey(BARRACK_HOTKEY)
		else
			b..setHotkey("V")

	// Spwan Slots
	let hotkeys = ["A","S","D","F"]
	for i = 0 to 3
		let slotID = slotIDs[i]
		new ChannelAbilityPreset(slotID,2,true)
			..setEditorSuffix("(Worker Hall)")
			..setName("Slot "+(i+1).toString())
			..presetIcon("ReplaceableTextures\\WorldEditUI\\DoodadPlaceholder.blp")
			// Footman
			..setTooltipNormal(1, "|cffffcc00Spawn slot "+(i+1).toString()+"|r : |cff33ff00Footman|r selected")
			..setTooltipNormalExtended(1, "Press to change which type is spawned each spawn cycle")
			// Archer
			..setTooltipNormal(2, "|cffffcc00Spawn slot "+(i+1).toString()+"|r : |cff33ff00Archer|r selected")
			..setTooltipNormalExtended(2, "Press to change which type is spawned each spawn cycle")
			// Hotkey
			..presetHotkey(hotkeys[i])
			// Button Placement
			..presetButtonPosNormal(i, 1)
			// Cost
			..presetCooldown(lvl -> 1)
			..presetManaCost(lvl -> 0)
			// Target
			..presetTargetTypes(Targettype.NONE)
			..presetOption(Option.TARGETIMAGE, true)

	// Footman Armor
	new UpgradeDefinition(BARRACK_FOOTMAN_ARMOR_ID)
		..setLevels(2)
		..presetEditorSuffix((int lvl) -> "(Barrack)")
		..presetName((int lvl) -> "Armor (|c90ecfc0eLevel "+lvl.toString()+"|r)")
		..presetTooltip((int lvl) -> "Armor (|c90ecfc0eLevel "+lvl.toString()+"|r)")
		..presetTooltipExtended((int lvl) -> "Increases the armor of your footmen")
		// Requirements
		// ..presetRequirements((int lvl) -> commaList(""))
		// ..presetRequirementsLevels((int lvl) -> commaList(""))
		// Hotkey
		..presetHotkey(lvl -> "X")
		// Icon
		..setIcon(1, Icons.bTNHumanArmorUpOne)
		..setIcon(2, Icons.bTNHumanArmorUpTwo)
		..setIcon(3, Icons.bTNHumanArmorUpThree)
		// Button Placement
		..setButtonPositionX(1)
		..setButtonPositionY(2)
		// Cost
		..setTimeBase(1)
		..setTimeIncrement(0)
		..setGoldCostBase(200)
		..setGoldCostIncrement(400)

	// Footman Damage
	new UpgradeDefinition(BARRACK_FOOTMAN_DAMAGE_ID)
		..setLevels(3)
		..presetEditorSuffix((int lvl) -> "(Barrack)")
		..presetName((int lvl) -> "Attack Damage (|c90ecfc0eLevel "+lvl.toString()+"|r)")
		..presetTooltip((int lvl) -> "Attack Damage (|c90ecfc0eLevel "+lvl.toString()+"|r)")
		..presetTooltipExtended((int lvl) -> "Increases the attack damage of your footmen")
		// Requirements
		// ..presetRequirements((int lvl) -> commaList(""))
		// ..presetRequirementsLevels((int lvl) -> commaList(""))
		// Hotkey
		..presetHotkey(lvl -> "C")
		// Icon
		..setIcon(1, "ReplaceableTextures\\CommandButtons\\BTNSteelMelee.blp")
		..setIcon(2, "ReplaceableTextures\\CommandButtons\\BTNThoriumMelee.blp")
		..setIcon(3, "ReplaceableTextures\\CommandButtons\\BTNArcaniteMelee.blp")
		// Button Placement
		..setButtonPositionX(2)
		..setButtonPositionY(2)
		// Cost
		..setTimeBase(1)
		..setTimeIncrement(0)
		..setGoldCostBase(200)
		..setGoldCostIncrement(400)

	// Archer Damage
	new UpgradeDefinition(BARRACK_ARCHER_DAMAGE_ID)
		..setLevels(3)
		..presetEditorSuffix((int lvl) -> "(Barrack)")
		..presetName((int lvl) -> "Attack Damage (|c90ecfc0eLevel "+lvl.toString()+"|r)")
		..presetTooltip((int lvl) -> "Attack Damage (|c90ecfc0eLevel "+lvl.toString()+"|r)")
		..presetTooltipExtended((int lvl) -> "Increases the attack damage of your archers")
		// Requirements
		// ..presetRequirements((int lvl) -> commaList(""))
		// ..presetRequirementsLevels((int lvl) -> commaList(""))
		// Hotkey
		..presetHotkey(lvl -> "Z")
		// Icon
		..setIcon(1, Icons.bTNImprovedBows)
		..setIcon(2, Icons.bTNImprovedBows)
		..setIcon(3, Icons.bTNImprovedBows)
		// Button Placement
		..setButtonPositionX(0)
		..setButtonPositionY(2)
		// Cost
		..setTimeBase(1)
		..setTimeIncrement(0)
		..setGoldCostBase(200)
		..setGoldCostIncrement(400)