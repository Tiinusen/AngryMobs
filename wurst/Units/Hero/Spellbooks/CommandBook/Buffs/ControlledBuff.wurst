package ControlledBuff

import HashMap
import ClosureTimers
import HeroConstants
import initlater SoldierEntity
import BuffObjEditing
import ProtectKingBuff
import initlater Hero

public let CONTROLLED_BUFF_OBJ = compiletime(createDummyBuffObject("Controlled", "This unit is controlled by a hero", Icons.bTNCallToArms, Abilities.generalAuraTarget, AttachmentPoints.origin))

let buffId = CONTROLLED_BUFF_OBJ.abilId

abstract class ControlledBuff
	private static HashMap<SoldierEntity, CallbackSingle> _buffMap = new HashMap<SoldierEntity, CallbackSingle>()

	static function has(SoldierEntity soldier) returns bool
		return _buffMap.has(soldier)

	static function add(SoldierEntity soldier, Hero hero, real duration)
		if _buffMap.has(soldier)
			let cb = _buffMap.get(soldier)
			if cb != null
				destroy cb
		if not soldier.getUnit().hasAbility(buffId)
			soldier.getUnit().addAbility(buffId)
		if duration > 0
			let cb = doAfter(duration) ->
				_remove(soldier, true)
			_buffMap.put(soldier, cb)
		else
			_buffMap.put(soldier, null)
		// Logic executed on buff creation
		Hero.removeControlledSoldier(soldier)
		ProtectKingBuff.remove(soldier)
		hero.addControlledSoldier(soldier)
		
		soldier.orderFollow(hero.getUnit())

	static function remove(SoldierEntity soldier)
		_remove(soldier, false)

	private static function _remove(SoldierEntity soldier, bool expired)
		if _buffMap.has(soldier)
			if not expired
				destroy _buffMap.get(soldier)
			_buffMap.remove(soldier)
			if soldier.getUnit().hasAbility(buffId)
				soldier.getUnit().removeAbility(buffId)
			// Logic executed on buff removal
			Hero.removeControlledSoldier(soldier)

			if soldier.isAlive()
				soldier.orderStop()
				soldier.decideNextAction()